// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
	/* set the register load if load asked, or if reset asked, ... */
	Or (a=load, b=reset, out=or1);		

	/* ... or if inc asked */
	Or (a=or1, b=inc, out=reginload);

	/* increment */ 
	Inc16 (in=currregval, out=outinc);

	/* check increment */
	Mux16 (a=currregval, b=outinc, sel=inc, out=in3);

	/* if load asked, use the input bus value, else work on the current register value */
	Mux16 (a=in3, b=in, sel=load, out=in4);					

	/* if reset */
	Mux16 (a=in4, b=false, sel=reset, out=regin);					

	Register (in=regin, load=reginload, out=currregval, out=out);
}
